.event_handler
\\************************************************************************************
\\CHARACTER IN INPUT BUFFER (2) EVENT HANDLER
	CMP #2; is A set with CHARACTER event number?
	BNE no_f9
	CPY #&89; 				f9 key
	BNE no_f9
	LDA flags
	ORA #%00010000;			set bit 4
	STA flags
	JMP (evntv_old)
.no_f9
	
\\************************************************************************************
\\**VSYNC (4) HANDLER
	CMP #4; is A set with the vsync event number?
	\ BNE no_vsync
	\\NEED TO SORT OUT BRANCHING
	BEQ vsync
	JMP no_vsync
.vsync
\\check if on-screen timer is ready
	LDA #%00100000; check bit 5
	BIT flags
	\ BEQ timer_not_ready
	BNE deal_with_timer
	
	JMP (evntv_old)
	
.deal_with_timer
\\check to see if counting up or down
	BIT flags2;					bit 7 (count up) is now in N
	BMI count_up

\\count down
	SEC 
	LDA hundredths
	SBC #2
	STA hundredths
	BCS check_zero
	LDA #98;					if a carry occurred, we reset to 98 (and not &FF)
	STA hundredths
	
	SEC
	LDA seconds;				and go on to deal with seconds
	SBC #1
	STA seconds
	BCS check_zero
	LDA #59;					if a carry occurred, we reset to 59 (and not &FF)
	STA seconds
	
	SEC
	LDA minutes;				and go on to deal with minutes
	SBC #1
	STA minutes

.check_zero
	LDA hundredths
	BNE display
	LDA seconds
	BNE display
	LDA minutes
	BNE display
	
	LDA flags2
	ORA #%10000000
	STA flags2
	LDA #129; alpha red
	STA bottom_row
	JMP display
	
.count_up
	LDX hundredths
	INX 
	INX
	STX hundredths
	CPX #100
	BCC display;				branch if carry clear (less than 100)
	LDX #0
	STX hundredths
	
	INC seconds
	LDA #60
	CMP seconds
	BNE display
	STX seconds
	
	INC minutes

.display	
	LDA #33
	STA column
	LDA minutes
	JSR print_dec
	LDA #36
	STA column
	LDA seconds
	JSR print_dec
	LDA #39
	STA column
	LDA hundredths
	JSR print_dec

	JMP (evntv_old)

.no_vsync

\\************************************************************************************
\\ESCAPE (6) EVENT HANDLER
	CMP #6; is A set with the ESCAPE event number?
	BNE no_escape
	LDA flags
	ORA #%00001000;			set bit 3 (escape)
	STA flags
.no_escape
	
	JMP (evntv_old)