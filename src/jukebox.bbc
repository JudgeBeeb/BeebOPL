5 REM===================================================================
6 REM
10 REM intial setup
15 ON ERROR GOTO 9000
100 CLS
200 MODE 7
201 old_himem=HIMEM
205 HIMEM=&2300
210 VDU 23;8202;0;0;0;:REM hide cursor
215	*FX 225,128
300 PRINT
400 PRINT CHR$(133);CHR$(157);CHR$(135);"f1:Play All";TAB(27);"ESCAPE:Stop"
500 PRINT CHR$(133);CHR$(157);CHR$(134);CHR$(141);TAB(16);"JUKE BOX"
600 PRINT CHR$(133);CHR$(157);CHR$(134);CHR$(141);TAB(16);"JUKE BOX"
700 PRINT CHR$(133);CHR$(157)
701 REM=================================================================
702 REM
710 REM open file, load track data and draw menu
800 handle%=OPENIN("JUKEDAT")
900 INPUT#handle%,tracks%
1000 DIM file$(tracks%), title$(tracks%)
1100 FOR n%=1 TO tracks%
1200 INPUT#handle%,file$(n%),title$(n%)
1300 IF (n% MOD 2)=0 THEN PRINT CHR$(135);CHR$(157);CHR$(133);CHR$(137);CHR$(n%+64);":";TAB(6);title$(n%) ELSE PRINT CHR$(133);CHR$(157);CHR$(134);CHR$(137);CHR$(n%+64);":";TAB(6);title$(n%)
1500 NEXT n%
1600 CLOSE#handle%
1610 FOR x%=tracks%+1 TO 9
1620 PRINT CHR$(135);CHR$(157)
1630 PRINT CHR$(133);CHR$(157);CHR$(134)
1640 NEXT x%
1650 REM=================================================================
1655 REM
1656 REM wait for key press
1700 REPEAT
1800 key%=GET
1805 IF key%=&81 GOSUB 10000
1900 selection%=key%-ASC("@")
2000 IF selection%<1 OR selection%>tracks% THEN GOTO 1800
2005 PRINT TAB(3,selection%+4);CHR$(136);
2100 OSCLI "JUKPLAY "+file$(selection%)
2105 PRINT TAB(3,selection%+4);CHR$(137);
2200 UNTIL FALSE

9000 REM===================================================================
9010 REM
9020 REM error handler
9030 IF ERR<>17 GOTO 9400
9020 FOR line%=1 TO tracks%
	9030 PRINT TAB(3,line%+4);CHR$(137);
9040 NEXT line%
9050 GOTO 1700
9400 REPORT
9410 PRINT " at line "; ERL
9420 END




10000 REM==================================================================
10001 REM
10009 REM routine to play all tracks
	10010 FOR all%=1 TO tracks%
		10020 PRINT TAB(3,all%+4);CHR$(136);
		10030 OSCLI "JUKPLAY "+file$(all%)
		10040 PRINT TAB(3,all%+4);CHR$(137);
		10050 pausetime=TIME+400
		10060 REPEAT UNTIL TIME>=pausetime
	10070 NEXT all%
10080 RETURN

